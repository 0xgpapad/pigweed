# Copyright 2020 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("$dir_pw_build/linker_script.gni")
import("$dir_pw_docgen/docs.gni")

if (dir_pw_boot_backend == dir_pw_boot_armv7m) {
  config("default_config") {
    include_dirs = [ "public" ]
  }

  pw_linker_script("armv7m_linker_script") {
    # pw_boot_armv7m_config is a scope provided by the target.
    assert(defined(pw_boot_armv7m_config),
           "pw_boot_armv7m depends on pw_boot_armv7m_config being defined!")
    defines = pw_boot_armv7m_config.defines
    linker_script = "basic_armv7m.ld"
  }

  source_set("pw_boot_armv7m") {
    public_configs = [ ":default_config" ]
    deps = [
      ":armv7m_linker_script",
      "$dir_pw_preprocessor",
    ]
    public = [
      "public/pw_boot_armv7m/boot.h",
    ]
    sources = [ "core_init.c" ] + public
  }
}

pw_doc_group("docs") {
  sources = [
    "docs.rst",
  ]
}
