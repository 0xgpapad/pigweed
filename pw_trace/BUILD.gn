# Copyright 2020 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("$dir_pw_build/facade.gni")
import("$dir_pw_docgen/docs.gni")
import("$dir_pw_unit_test/test.gni")

config("default_config") {
  include_dirs = [ "public" ]
}

pw_facade("pw_trace") {
  backend = dir_pw_trace_backend
  public_configs = [ ":default_config" ]
  public = [
    "public/pw_trace/internal/trace_internal.h",
    "public/pw_trace/trace.h",
  ]
  deps = [ dir_pw_preprocessor ]
}

pw_test_group("tests") {
  tests = [ ":trace_facade_test" ]
  if (dir_pw_trace_backend != "") {
    tests += [ ":trace_backend_compile_test" ]
  }
}

pw_test("trace_facade_test") {
  configs = [ ":default_config" ]
  sources = [ "trace_facade_test.cc" ]
  public = [
    "public/pw_trace/internal/trace_internal.h",
    "public/pw_trace/trace.h",
    "pw_trace_test/fake_backend.h",
    "pw_trace_test/public_overrides/pw_trace_backend/trace_backend.h",
  ]
  include_dirs = [
    "pw_trace_test",
    "pw_trace_test/public_overrides",
  ]
}

pw_test("trace_backend_compile_test") {
  enable_if = dir_pw_trace_backend != ""

  deps = [
    ":pw_trace",
    dir_pw_trace_backend,
  ]

  sources = [
    "trace_backend_compile_test.c",
    "trace_backend_compile_test.cc",
  ]
}

pw_doc_group("docs") {
  sources = [ "docs.rst" ]
}
