# Copyright 2020 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

# gn-format disable
import("//build_overrides/pigweed.gni")

import("$dir_pw_build/target_types.gni")
import("$dir_pw_docgen/docs.gni")
import("$dir_pw_unit_test/test.gni")
config("default_config") {
  include_dirs = [ "public" ]
}

pw_source_set("pw_hdlc_lite") {
  public_configs = [ ":default_config" ]
  public = [
    "public/pw_hdlc_lite/decoder.h",
    "public/pw_hdlc_lite/encoder.h",
    "public/pw_hdlc_lite/sys_io_stream.h",
  ]
  sources = [
    "decoder.cc",
    "encoder.cc",
    "pw_hdlc_lite_private/protocol.h",
  ]
  public_deps = [
    dir_pw_bytes,
    dir_pw_result,
    dir_pw_status,
    dir_pw_stream,
    dir_pw_sys_io,
  ]
  deps = [
    dir_pw_checksum,
    dir_pw_log,
  ]
  friend = [ ":*" ]
}

pw_source_set("pw_rpc") {
  public_configs = [ ":default_config" ]
  public = [
    "public/pw_hdlc_lite/hdlc_channel.h",
    "public/pw_hdlc_lite/rpc_packets.h",
  ]
  sources = [ "hdlc_channel.cc" ]
  public_deps = [
    "$dir_pw_rpc:server",
    dir_pw_stream,
  ]
  deps = [
    ":pw_hdlc_lite",
    dir_pw_checksum,
    dir_pw_log,
  ]
}

pw_test_group("tests") {
  tests = [
    ":encoder_test",
    ":decoder_test",
    ":hdlc_channel_test",
  ]
  group_deps = [
    "$dir_pw_preprocessor:tests",
    "$dir_pw_span:tests",
    "$dir_pw_status:tests",
    "$dir_pw_stream:tests",
  ]
}

pw_test("encoder_test") {
  deps = [ ":pw_hdlc_lite" ]
  sources = [ "encoder_test.cc" ]
}

action("generate_decoder_test") {
  outputs = [ "$target_gen_dir/generated_decoder_test.cc" ]
  script = "py/decoder_test.py"
  args = [ "--generate-cc-decode-test" ] + rebase_path(outputs)
}

pw_test("decoder_test") {
  deps = [
    ":generate_decoder_test",
    ":pw_hdlc_lite",
  ]
  sources = [ "decoder_test.cc" ] + get_target_outputs(":generate_decoder_test")
}

pw_test("hdlc_channel_test") {
  deps = [
    ":pw_hdlc_lite",
    ":pw_rpc",
  ]
  sources = [ "hdlc_channel_test.cc" ]
}

pw_doc_group("docs") {
  sources = [ "docs.rst" ]
}
