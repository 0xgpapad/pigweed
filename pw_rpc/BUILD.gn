# Copyright 2020 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("$dir_pw_docgen/docs.gni")
import("$dir_pw_protobuf_compiler/proto.gni")
import("$dir_pw_unit_test/test.gni")

config("default_config") {
  include_dirs = [ "public" ]
}

source_set("pw_rpc") {
  deps = [ ":protos_pwpb" ]
  public_configs = [ ":default_config" ]
  public_deps = [
    dir_pw_assert,
    dir_pw_log,
    dir_pw_span,
    dir_pw_status,
  ]
  public = [
    "public/pw_rpc/channel.h",
    "public/pw_rpc/server.h",
  ]
  sources = [
    "channel.cc",
    "packet.cc",
    "public/pw_rpc/internal/packet.h",
    "public/pw_rpc/internal/service.h",
    "public/pw_rpc/internal/service_registry.h",
    "server.cc",
    "service.cc",
    "service_registry.cc",
  ]
  friend = [ ":*" ]
}

pw_proto_library("protos") {
  sources = [ "pw_rpc_protos/packet.proto" ]
}

pw_doc_group("docs") {
  sources = [ "docs.rst" ]
}

pw_test_group("tests") {
  tests = [
    ":packet_test",
    ":server_test",
  ]
}

pw_test("server_test") {
  deps = [
    ":protos_pwpb",
    ":pw_rpc",
  ]
  sources = [ "server_test.cc" ]
}

pw_test("packet_test") {
  deps = [
    ":pw_rpc",
    dir_pw_protobuf,
  ]
  sources = [ "packet_test.cc" ]
}
