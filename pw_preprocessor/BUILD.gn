# Copyright 2019 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("$dir_pw_unit_test/test.gni")

config("default_config") {
  include_dirs = [ "public" ]
}

source_set("pw_preprocessor") {
  public_configs = [
    "$dir_pw_build:pw_default_cpp",
    ":default_config",
  ]
  public = [
    "public/pw_preprocessor/boolean.h",
    "public/pw_preprocessor/compiler.h",
    "public/pw_preprocessor/concat.h",
    "public/pw_preprocessor/macro_arg_count.h",
    "public/pw_preprocessor/util.h",
  ]
  sources = public
}

# All pw_preprocessor test binaries.
group("unit_tests") {
  deps = [
    ":boolean_test",
    ":concat_test",
    ":macro_arg_count_test",
    ":util_test",
  ]
}

template("preprocessor_test") {
  not_needed([ "invoker" ])
  pw_test(target_name) {
    deps = [
      ":pw_preprocessor",
      "$dir_pw_unit_test:main",
    ]
    sources = [
      "$target_name.cc",
    ]
  }
}

preprocessor_test("boolean_test") {
}
preprocessor_test("concat_test") {
}
preprocessor_test("macro_arg_count_test") {
}
preprocessor_test("util_test") {
}
