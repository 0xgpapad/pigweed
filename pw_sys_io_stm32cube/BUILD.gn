# Copyright 2021 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pw_build/target_types.gni")
import("$dir_pw_docgen/docs.gni")

# Defaults to USART1 on the STM32F429xx, but can be overridden.
declare_args() {
  # USART to use for sys_io
  pw_sys_io_stm32cube_usart_num = "1"

  # GPIO Port of the specified USART
  pw_sys_io_stm32cube_gpio_port = "A"

  # GPIO Pin for tx
  pw_sys_io_stm32cube_gpio_tx_pin = "9"

  # GPIO Pin for rx
  pw_sys_io_stm32cube_gpio_rx_pin = "10"
}

config("public_includes") {
  include_dirs = [ "public" ]
}

pw_source_set("pw_sys_io_stm32cube") {
  defines = [
    "USART_NUM=$pw_sys_io_stm32cube_usart_num",
    "USART_GPIO_PORT_CHAR=$pw_sys_io_stm32cube_gpio_port",
    "USART_TX_PIN_NUM=$pw_sys_io_stm32cube_gpio_tx_pin",
    "USART_RX_PIN_NUM=$pw_sys_io_stm32cube_gpio_rx_pin",
  ]
  public_configs = [ ":public_includes" ]
  public_deps = [
    "$dir_pw_preprocessor",
    "$dir_pw_status",
    "$dir_pw_third_party/stm32cube",
  ]
  public = [ "public/pw_sys_io_stm32cube/init.h" ]
  sources = [ "sys_io.cc" ]
  deps = [
    "$dir_pw_sys_io:default_putget_bytes",
    "$dir_pw_sys_io:facade",
  ]
}
