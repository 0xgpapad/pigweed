# Copyright 2020 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("//build_overrides/pigweed.gni")

import("$dir_pw_build/facade.gni")
import("$dir_pw_build/target_types.gni")
import("$dir_pw_docgen/docs.gni")
import("$dir_pw_unit_test/test.gni")
import("backend.gni")

config("public_include_path") {
  include_dirs = [ "public" ]
  visibility = [ ":*" ]
}

pw_facade("id") {
  backend = pw_thread_ID_BACKEND
  public_configs = [ ":public_include_path" ]
  public = [ "public/pw_thread/id.h" ]
}

pw_facade("sleep") {
  backend = pw_thread_SLEEP_BACKEND
  public_configs = [ ":public_include_path" ]
  public = [ "public/pw_thread/sleep.h" ]
  public_deps = [
    "$dir_pw_chrono:system_clock",
    "$dir_pw_preprocessor",
  ]
  sources = [ "sleep.cc" ]
}

pw_facade("yield") {
  backend = pw_thread_YIELD_BACKEND
  public_configs = [ ":public_include_path" ]
  public = [ "public/pw_thread/yield.h" ]
  public_deps = [ "$dir_pw_preprocessor" ]
  sources = [ "yield.cc" ]
}

pw_test_group("tests") {
  tests = [
    ":id_facade_test",
    ":sleep_facade_test",
    ":yield_facade_test",
  ]
}

pw_test("id_facade_test") {
  enable_if = pw_thread_ID_BACKEND != ""
  sources = [ "id_facade_test.cc" ]
  deps = [ ":id" ]
}

pw_test("sleep_facade_test") {
  enable_if = pw_thread_SLEEP_BACKEND != "" && pw_thread_ID_BACKEND != ""
  sources = [
    "sleep_facade_test.cc",
    "sleep_facade_test_c.c",
  ]
  deps = [
    ":id",
    ":sleep",
    "$dir_pw_chrono:system_clock",
  ]
}

pw_test("yield_facade_test") {
  enable_if = pw_thread_YIELD_BACKEND != "" && pw_thread_ID_BACKEND != ""
  sources = [
    "yield_facade_test.cc",
    "yield_facade_test_c.c",
  ]
  deps = [
    ":id",
    ":yield",
  ]
}

pw_doc_group("docs") {
  sources = [ "docs.rst" ]
}
