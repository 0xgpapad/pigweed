# Copyright 2020 The Pigweed Authors
#
# Licensed under the Apache License, Version 2.0 (the "License"); you may not
# use this file except in compliance with the License. You may obtain a copy of
# the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
# WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the
# License for the specific language governing permissions and limitations under
# the License.

import("$dir_pw_docgen/docs.gni")
import("$dir_pw_unit_test/test.gni")

config("default_config") {
  include_dirs = [ "public" ]
}

config("backend_config") {
  include_dirs = [ "public_overrides" ]
}

source_set("pw_log_tokenized") {
  public_configs = [
    ":backend_config",
    ":default_config",
  ]
  public_deps = [ "$dir_pw_tokenizer:global_handler_with_payload" ]
  deps = [ ":core" ]
  public = [
    "public/pw_log_tokenized/log_tokenized.h",
    "public_overrides/pw_log_backend/log_backend.h",
  ]
}

source_set("core") {
  public_configs = [ ":default_config" ]
  public_deps = [
    "$dir_pw_log:facade",
    dir_pw_preprocessor,
  ]
  visibility = [ ":*" ]
}

pw_test_group("tests") {
  tests = [ ":test" ]
}

pw_test("test") {
  sources = [ "test.cc" ]
  deps = [ ":pw_log_tokenized" ]

  # TODO(hepler): Switch this to a facade test when available.
  enable_if = dir_pw_tokenizer_global_handler_with_payload_backend == ""
}

pw_doc_group("docs") {
  sources = [ "docs.rst" ]
}
